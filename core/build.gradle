

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:1.3.1'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
//apply plugin: 'signing'
apply plugin: 'retrolambda'

apply from: "$rootDir/lib.gradle"

defaultTasks 'build'

jar {
	baseName project.projectName
	version project.fjVersion
	manifest {
		attributes 'Signature-Version': project.fjVersion
	}
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.5'
    testCompile "junit:junit:4.11"
}

task javadoc(type: Javadoc, overwrite: true, dependsOn: "java8Javadoc") {
}

retrolambda {
    jdk System.getenv("JAVA8_HOME")
    oldJdk System.getenv("JAVA7_HOME")
    javaVersion JavaVersion.VERSION_1_7
}

task javadocJar(type: Jar, dependsOn: java8Javadoc) {
    classifier = 'javadoc'
    from "build/docs/javadoc"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

Boolean doSigning() {
    signingEnabled.trim() == "true"
}

if (doSigning()) {
    apply plugin: "signing"
    signing {
       sign configurations.archives
    }
}

uploadArchives.enabled = true

