
apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = "1.6"
targetCompatibility = "1.6"

archivesBaseName = project.projectName

jar {
	version project.fjVersion
	manifest {
		attributes 'Signature-Version': project.fjVersion
	}
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.5'
    testCompile "junit:junit:4.11"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from "build/docs/javadoc"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

Boolean doSigning() {
    signingEnabled.trim() == "true"
}


if (doSigning()) {
    apply plugin: "signing"
    signing {
        sign configurations.archives
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            // used for signing the artifacts, no need to sign snapshot, so leave out for now
//            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            if (doSigning()) {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            }

            repository(url: sonatypeUploadUrl) {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            pom {
                groupId = project.group
                project {
                    name pomProjectName
                    packaging 'jar'
                    description projectDescription
                    url projectUrl
                    organization {
                        name pomOrganisation
                        url projectUrl
                    }
                    scm {
                        url scmUrl
//                        connection scmGitFile
//                        developerConnection scmGitFile
                    }
                    licenses {
                        license {
                            name "The BSD3 License"
                            url "https://github.com/functionaljava/functionaljava/blob/master/etc/LICENCE"
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            email primaryEmail
                        }
                    }
                }
            }
        }
    }
}
